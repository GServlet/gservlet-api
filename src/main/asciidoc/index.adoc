= GServlet Developer Guide
Mamadou Lamine Ba <https://github.com/laminba2003[@laminba2003]>
:revnumber: {project-version}
:example-caption!:
ifndef::imagesdir[:imagesdir: images]
ifndef::sourcedir[:sourcedir: ../../main/java]
:description: This developer guide describes how to use the GServlet API.
:keywords: Java, Servlets, Groovy, Spring, reference, learn, how to
:doctype: book
:page-layout!:
:toc: left
:nofooter:
:sectanchors:
:sectlinks:
:sectnums:
:icons: font
:source-highlighter: highlightjs
:source-language: asciidoc
:docinfo: shared-head

== Introduction

https://github.com/GServlet/gservlet-api[GServlet] is an open source project inspired from the http://docs.groovy-lang.org/latest/html/documentation/servlet-userguide.html[Groovlets], which aims to use the Groovy language and its provided modules to simplify Servlet API web development. Groovlets are Groovy scripts executed by a servlet. They are run on request, having the whole web context (request, response, etc.) bound to the evaluation context. They are much more suitable for smaller web applications. Compared to Java Servlets, coding in Groovy can be much simpler. It has a couple of implicit variables we can use, for example, _request_, _response_ to access the https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpServletRequest.html[HttpServletRequest], and https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpServletResponse.html[HttpServletResponse] objects. We have access to the https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] with the _session_ variable. If we want to output data, we can use _out_, _sout_, and _html_.

.Groovlet.groovy 
[#src-listing] 
[source,java]  
----
if (!session) {
    session = request.getSession(true)
}
if (!session.counter) {
    session.counter = 1
}
html.html {
  head {
      title('Groovy Servlet')
  }
  body {
    p("Hello, ${request.remoteHost}: ${session.counter}! ${new Date()}")
  }
}
session.counter = session.counter + 1
----

The same philosophy has been followed in the design of this API and if you have no prior experience with the Groovy language, there are several curated lists of online resources for learning it.

* https://groovy-lang.org[Official Groovy Website]
* https://groovy-lang.org/learn.html#books[List of Programming Books]

== Main Features

* Servlet 3.1+ Support
* Groovy Scripting and Hot Reloading
* JSON, HTML and JDBC Support

== Requirements

* Java 8+
* Java IDE (Eclipse, IntelliJ IDEA, NetBeans..)
* Java EE 7+ compliant WebServer (Tomcat, Wildfly, Glassfish, Payara..)


== Installation

To get started with GServlet, you may want to begin by creating your first project. This section shows you how to get up and running quickly. It is highly recommended to consume the API through a dependency management tool and the artifact can be found in Maven's central repository. It is named *_gservlet-api_* and you just need to name a dependency on it in your project.

=== Maven

.pom.xml 
[#src-listing] 
[source,xml]  
---- 
<dependency>
	<groupId>org.gservlet</groupId>
	<artifactId>gservlet-api</artifactId>
	<version>1.0.0</version>
</dependency>
----

=== Gradle

.build.gradle 
[#src-listing] 
[source,java]  
---- 
repositories {
    mavenCentral()
}

dependencies {
    compile("org.gservlet:gservlet-api:1.0.0")
}
----

## Getting Started

Once your Java web server is installed and configured, you can put it to work. Five steps take you from writing your first Groovy servlet to running it. Using Maven, these steps are as follows:

image::maven-project.png[Maven webapp project]

. Create a Maven webapp https://maven.apache.org/archetypes/maven-archetype-webapp[project]
. Create the *_groovy_* folder inside your webapp directory
. Write the servlet source code
. Run your Java web server
. Call your servlet from a web browser

Below are some examples that you can try out. 
    
.ProjectServlet.groovy 
[#src-listing] 
[source,java]  
----
import org.gservlet.annotation.Servlet

@Servlet("/projects")
class ProjectServlet {

	List projects = []

	void init() {
	   projects << [id : 1, name : "Groovy", url : "https://groovy-lang.org"]
	   projects << [id : 2, name : "Spring", url : "https://spring.io"]
	   projects << [id : 3, name : "Maven",  url : "https://maven.apache.org"]
	}

	void get() {
	   json(projects)
	}

	void post() {
	   def project = request.body
	   projects << project
	   json(project)
	}

	void put() {
	   def project = request.body
	   int index = projects.findIndexOf { it.id == project.id }
	   projects[index] = project
	   json(project)
	}

	void delete() {
	   def project = request.body
	   int index = projects.findIndexOf { it.id == project.id }
	   json(projects.remove(index))
	}
	
}
----

.CorsFilter.groovy 
[#src-listing] 
[source,java]  
----
import org.gservlet.annotation.Filter

@Filter("/*")
class CorsFilter {

    void filter() {
      response.addHeader("Access-Control-Allow-Origin", "*")
      response.addHeader("Access-Control-Allow-Methods","GET, OPTIONS, HEAD, PUT, POST, DELETE")
      if (request.method == "OPTIONS") {
        response.status = response.SC_ACCEPTED
        return
      }
      next()
    }

}
----

.ServletRequestListener.groovy 
[#src-listing] 
[source,java]  
----
import org.gservlet.annotation.RequestListener

@RequestListener
class ServletRequestListener {

   void init() {
     println "request initialized"
   }

   void destroy() {
     println "request destroyed"
   }

}
----

NOTE: The use of the default package is discouraged and for a hot reloading of your source code, set the **GSERVLET_RELOAD** environment variable to true in your IDE.    

== Creating Servlets

A servlet is a small Java program that runs within a Web server. The https://docs.oracle.com/javaee/7/api/javax/servlet/Servlet.html[Servlet] interface defines methods that all servlets must implement. To implement this interface, you can write a generic servlet that extends the https://docs.oracle.com/javaee/7/api/javax/servlet/GenericServlet.html[GenericServlet] class or an HTTP servlet which extends the https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpServlet.html[HttpServlet] class and overrides at least one method, usually one of these:

* _doGet_, for HTTP GET requests
* _doPost_, for HTTP POST requests
* _doPut_, for HTTP PUT requests
* _doDelete_, for HTTP DELETE requests

This is a Java class that extends the HttpServlet class:

.HelloWordServlet.java 
[#src-listing] 
[source,java]  
---- 
import javax.servlet.annotation.WebServlet;
import javax.servlet.http.HttpServlet;
import javax.servlet.http.HttpServletRequest;
import javax.servlet.http.HttpServletResponse;
import java.io.IOException;

@WebServlet("/index.html")
public class HelloWordServlet extends HttpServlet {
	
   public void doGet(HttpServletRequest request,HttpServletResponse response) throws IOException {  
      response.setContentType("text/html");  
      PrintWriter out = response.getWriter();
      out.println("<html>");
      out.println("<body>");
      out.println("<p>Hello World!</p>");
      out.println("</body>");
      out.println("</html>");  
   }
	
}
----

Now, we are going to write its Groovy counterpart with the GServlet API so you can perceive the difference in terms of simplicity and clarity. The name of the HTTP request method handlers are shortened to _get_, _post_ and so on. They take no arguments since the request and the response are now implicit variables.

.HelloWordServlet.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.Servlet

@Servlet("/index.html")
class HelloWordServlet {

   void get() {
      out.println("<html>")
      out.println("<body>")
      out.println("<p>Hello World!</p>")
      out.println("</body>")
      out.println("</html>")     
   }
	
}
----

By default the content type of the https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpServletResponse.html[HttpServletResponse] is set to _text/html_ and the implicit _out_ variable used to generate the HMTL content is nothing less than a reference to its https://docs.oracle.com/javaee/7/api/javax/servlet/ServletResponse.html#getWriter[PrintWriter] object. We could use as well the implicit _html_ variable which is an instance of a Groovy MarkupBuilder, to write a better version of this servlet.


.HelloWordServlet.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.Servlet

@Servlet(value="/index.html", loadOnStartup = 1)
class HelloWordServlet {

  void get() {
     html.html {
       body {
         p("Hello World!")
       }
     } 
  }
   
}
----

The generated HTML content looks like this: 

.Generated HTML 
[#src-listing] 
[source,html]  
---- 
<!DOCTYPE html>
<html>
  <body>
    <p>Hello World!</p>
  </body>
</html>
----

=== Servlet annotation attributes

There are the same as those of the https://docs.oracle.com/javaee/7/api/javax/servlet/annotation/WebServlet.html[@WebServlet] annotation.

|===
|Name | Type | Description

| _name_ | String | name of the servlet

| _value_ | String[] | URL patterns of the servlet

| _urlPatterns_ |  String[] | URL patterns of the servlet

| _loadOnStartup_ | Integer | load-on-startup order of the servlet
 
| _initParams_ | InitParam[] | init parameters of the servlet

| _asyncSupported_ | boolean | Declares whether the servlet supports asynchronous operation mode

| _smallIcon_ | String | small icon of the servlet

| _largeIcon_ | String | large icon of the servlet

| _description_ | String | description of the servlet

| _displayName_ | String | display name of the servlet

|===


=== Servlet implicit variables

The implicit variables made available to a https://docs.oracle.com/javaee/7/api/javax/servlet/Servlet.html[Servlet] are as follows:

|===
|Variable |Description

|_logger_ | https://docs.oracle.com/javase/7/docs/api/java/util/logging/Logger.html[logger] object

|_config_ | https://docs.oracle.com/javaee/7/api/javax/servlet/ServletConfig.html[ServletConfig] object

|_request_ |  https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpServletRequest.html[HttpServletRequest] object

|_response_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpServletResponse.html)[HttpServletResponse] object
 
|_session_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] object

|_context_ | https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContext.html[ServletContext] object

|_sql_ | http://docs.groovy-lang.org/latest/html/api/groovy/sql/Sql.html[Sql] object

|_out_ | https://docs.oracle.com/javase/7/docs/api/java/io/PrintWriter.html[PrintWriter] object

|_html_ | http://docs.groovy-lang.org/latest/html/api/groovy/xml/MarkupBuilder.html[MarkupBuilder] object
|===


=== Servlet methods

For an exhaustive list of the supported methods, please read the Javadocs.

|===
|Method |Description

|_void init()_ | handles the initialization process

|_void get()_ | handles the GET request

|_void post()_ | handles the POST request

|_void put()_ |  handles the PUT request

|_void delete()_ | handles the DELETE request
 
|_void head()_ | handles the HEAD request

|_void options()_ | handles the OPTIONS request

|_void trace()_ | handles the TRACE request.

|_void forward(location)_ |  Forwards the request to the provided location

|_void redirect(location)_ |  Redirects the request to the provided location

|_void json(object)_ |  Sends the response as JSON

|_void destroy()_ |  invoked when taken out of the service

|===

=== Servlet initialization parameters

Since Servlet 3, the https://docs.oracle.com/javaee/7/api/javax/servlet/annotation/WebInitParam.html[@WebInitParam] annotation is used to specify initialization parameters for a servlet programmatically, and it takes a required name and value. You can add a description but this is rather informative. In the initialization method _init()_, we can get our parameters using the _getInitParameter()_ method of the https://docs.oracle.com/javaee/7/api/javax/servlet/ServletConfig.html[ServletConfig] object. In the GServlet API, the annotation has been shorten to _@InitParam_ and you can get an initialization parameter through the implicit _config_ variable using just its name or as described above.

.UploadServlet.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.InitParam
import org.gservlet.annotation.Servlet

@Servlet( urlPatterns = "/upload",
initParams = [
	@InitParam(name = "uploadDirectory", value = "/images")
] )
class UploadServlet {

	void init() {
		println config.uploadDirectory
		println config.getInitParameter("uploadDirectory")
	}

}
----

The attributes of the _@InitParam_ annotation are the same as those of the https://docs.oracle.com/javaee/7/api/javax/servlet/annotation/WebInitParam.html[@WebInitParam] annotation.

|===
|Name | Type | Description

| _name_ | String | name of the initialization parameter

| _value_ | String | value of the initialization parameter

|===

=== Servlet Multipart configuration

Supporting file uploads is a very basic and common requirement for many web applications. Prior to Servlet 3.0, implementing file upload required the use of external libraries or complex input processing. Version 3.0 of the Java Servlet specification helps to provide a viable solution to the problem in a generic and portable way. The Servlet 3.0 specification supports file upload out of the box, so any web container that implements the specification can parse multipart requests and make mime attachments available through the https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpServletRequest.html[HttpServletRequest] object. A new annotation, https://docs.oracle.com/javaee/7/api/javax/servlet/annotation/MultipartConfig.html[@MultipartConfig], is used to indicate that the servlet on which it is declared expects requests to made using the _multipart/form-data_ MIME type. Therefore, it can retrieve the Part components of a given _multipart/form-data_ request by calling the _getPart(String name)_ or _getParts()_ method of the https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpServletRequest.html[HttpServletRequest] object.

.UploadServlet.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.InitParam
import org.gservlet.annotation.Servlet
import javax.servlet.annotation.MultipartConfig

@Servlet( urlPatterns = "/upload",
initParams = [
	@InitParam(name = "uploadDirectory", value = "/images")
] )
@MultipartConfig( fileSizeThreshold = 1048576, maxFileSize = 5242880L, maxRequestSize = 26214400L )
class UploadServlet {

	String uploadPath

	void init() {
		uploadPath = context.getRealPath(config.uploadDirectory)
		File uploadDir = new File(uploadPath)
		if (!uploadDir.exists()) {
			uploadDir.mkdir()
		}
	}

	void get() {
		File uploadDir = new File(uploadPath)
		def files = []
		uploadDir.listFiles()?.each { file -> 
			files << [name : file.name, length : file.length(), lastModified : file.lastModified()]
		}
		json(files);
	}

	void post() {
		request.getParts().each { part ->
			String file = uploadPath + File.separator + request.getFileName(part)
			part.write(file)
		}
		redirect(context.contextPath + "/upload");
	}


}
----

=== Servlet Security

The https://docs.oracle.com/javaee/7/api/javax/servlet/annotation/ServletSecurity.html[@ServletSecurity] annotation is used to specify security constraints on a Java servlet. The annotations https://docs.oracle.com/javaee/7/api/javax/servlet/annotation/HttpMethodConstraint.html[@HttpMethodConstraint] and https://docs.oracle.com/javaee/7/api/javax/servlet/annotation/HttpConstraint.html[@HttpConstraint] are used within that annotation to define the security constraints.

 
[#src-listing] 
[source,java]  
---- 
@ServletSecurity(
    httpMethodConstraints = <HttpMethodConstraint[]>,
    value = <HttpConstraint>
)
----

The _httpMethodConstraints_ attribute specifies one or more constraints for some specific HTTP methods, whereas the _value_ attribute specifies a constraint that applies for all other HTTP methods.

.Encryption for all HTTP methods 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.Servlet
import javax.servlet.annotation.ServletSecurity
import javax.servlet.annotation.ServletSecurity.TransportGuarantee
import javax.servlet.annotation.HttpConstraint

@Servlet(value="/projects")
@ServletSecurity(@HttpConstraint(transportGuarantee = TransportGuarantee.CONFIDENTIAL))
class ProjectServlet {

	
}
----


.Denying access to HTTP POST method 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.Servlet
import javax.servlet.annotation.ServletSecurity
import javax.servlet.annotation.HttpMethodConstraint
import javax.servlet.annotation.ServletSecurity.EmptyRoleSemantic

@Servlet(value="/projects")
@ServletSecurity(httpMethodConstraints = @HttpMethodConstraint(value = "POST", 
	emptyRoleSemantic = EmptyRoleSemantic.DENY))
class ProjectServlet {

	
}
----


.Requiring that users must have the admin role 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.Servlet
import javax.servlet.annotation.ServletSecurity
import javax.servlet.annotation.HttpMethodConstraint

@Servlet(value="/projects")
@ServletSecurity(
 httpMethodConstraints = [
  @HttpMethodConstraint(value = "GET", rolesAllowed = "admin"),
  @HttpMethodConstraint(value = "POST", rolesAllowed = "admin"),      
 ]
)
class ProjectServlet {

	
}
----

You can find more examples about how to use the https://docs.oracle.com/javaee/7/api/javax/servlet/annotation/ServletSecurity.html[@ServletSecurity] annotation on the web.

== Creating Filters

A filter is an object that performs filtering tasks on either the request to a resource (a servlet or static content), or on the response from a resource, or both. The https://docs.oracle.com/javaee/7/api/javax/servlet/Filter.html[Filter] interface defines methods that all filters must implement. Filters perform filtering in the _doFilter()_ method.

This is a Java class that implements this interface:

.MyFilter.java 
[#src-listing] 
[source,java]  
----  
import javax.servlet.annotation.WebFilter;
import javax.servlet.Filter;
import javax.servlet.FilterChain;
import javax.servlet.FilterConfig;
import javax.servlet.ServletRequest;
import javax.servlet.ServletResponse;
import javax.servlet.ServletException;
import java.io.IOException;
 
@WebFilter("/*")
public class MyFilter implements Filter {
 
    public void init(FilterConfig filterConfig) throws ServletException {  
    }
 
    public void doFilter(ServletRequest request, ServletResponse response, FilterChain chain)
            throws IOException, ServletException {  
        chain.doFilter(request, response);
    }
 
    public void destroy() {
    }
    
}
----

The Groovy version of this filter with the GServlet API looks like this:

.MyFilter.groovy 
[#src-listing] 
[source,java]  
----  
import org.gservlet.annotation.Filter;
 
@Filter("/*")
class MyFilter {
 
    void init() {  
    }
 
    void filter() {
        next()
    }
 
    void destroy() {
    }
    
}
----

=== Filter annotation attributes

There are the same as those of the https://docs.oracle.com/javaee/7/api/javax/servlet/annotation/WebFilter.html[@WebFilter] annotation.

|===
|Name | Type | Description

| _filterName_ | String | name of the filter

| _value_ | String[] | URL patterns of the filter

| _urlPatterns_ |  String[] | URL patterns of the filter

| _dispatcherTypes_ | DispatcherType[] | dispatcher types to which the filter applies
 
| _initParams_ | InitParam[] | init parameters of the filter

| _servletNames_ | String[] | names of the servlets to which the filter applies

| _asyncSupported_ | boolean | Declares whether the filter supports asynchronous operation mode

| _smallIcon_ | String | small icon of the filter

| _largeIcon_ | String | large icon of the filter

| _description_ | String | description of the filter

| _displayName_ | String | display name of the filter

|===


=== Filter implicit variables

The implicit variables made available to a https://docs.oracle.com/javaee/7/api/javax/servlet/Filter.html[Filter] are as follows:

|===
|Variable |Description

|_logger_ | https://docs.oracle.com/javase/7/docs/api/java/util/logging/Logger.html[logger] object

|_config_ | https://docs.oracle.com/javaee/7/api/javax/servlet/FilterConfig.html[FilterConfig] object

|_request_ |  https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpServletRequest.html[HttpServletRequest] object

|_response_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpServletResponse.html)[HttpServletResponse] object

|_chain_ | https://docs.oracle.com/javaee/7/api/javax/servlet/FilterChain.html)[FilterChain] object
 
|_session_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] object

|_context_ | https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContext.html[ServletContext] object

|_sql_ | http://docs.groovy-lang.org/latest/html/api/groovy/sql/Sql.html[Sql] object

|_out_ | https://docs.oracle.com/javase/7/docs/api/java/io/PrintWriter.html[PrintWriter] object

|_html_ | http://docs.groovy-lang.org/latest/html/api/groovy/xml/MarkupBuilder.html[MarkupBuilder] object
|===

=== Filter methods

For an exhaustive list of the supported methods, please read the Javadocs. 

|===
|Method |Description

|_void init()_ | handles the initialization process

|_void filter()_ | handles the filtering tasks

|_void next()_ | Calls the next filter in the chain

|_void json(object)_ |  Sends the response as JSON

|_void destroy()_ |  invoked when taken out of the service

|===


=== Filter initialization parameters

The https://docs.oracle.com/javaee/7/api/javax/servlet/annotation/WebInitParam.html[@WebInitParam] annotation is used to specify initialization parameters for a filter programmatically. In its initialization method _init()_, we can get our parameters using the _getInitParameter()_ method of the https://docs.oracle.com/javaee/7/api/javax/servlet/FilterConfig.html[FilterConfig] object. Like for a servlet, the annotation has been shorten to _@InitParam_ and you can get an initialization parameter through the implicit _config_ variable using just its name or as described above.

.LoggingFilter.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.InitParam
import org.gservlet.annotation.Filter

@Filter( value = "/*",
initParams = [
	@InitParam(name = "loggingDirectory", value = "/logs")
])
class LoggingFilter {

	void init() {
		println config.loggingDirectory
		println config.getInitParameter("loggingDirectory")
	}

}
----


== Creating Listeners

During the lifetime of a typical Java EE web application, a number of events take place. The Servlet API provides a number of listener interfaces that we can implement to react to these events.

|=== 

| https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContextListener.html[ServletContextListener] | Interface for receiving notification events about https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContext.html[ServletContext] lifecycle changes.

| https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContextAttributeListener.html[ServletContextAttributeListener] | Interface for receiving notification events about https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContext.html[ServletContext] attribute changes.

| https://docs.oracle.com/javaee/7/api/javax/servlet/ServletRequestListener.html[ServletRequestListener] | Interface for receiving notification events about a https://docs.oracle.com/javaee/7/api/javax/servlet/ServletRequest.html[ServletRequest] coming into and going out of scope of a web application.

| https://docs.oracle.com/javaee/7/api/javax/servlet/ServletRequestAttributeListener.html[ServletRequestAttributeListener] | Interface for receiving notification events about https://docs.oracle.com/javaee/7/api/javax/servlet/ServletRequest.html[ServletRequest] attribute changes.

| https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionListener.html[HttpSessionListener] | Interface for receiving notification events about https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] lifecycle changes.

| https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionAttributeListener.html[HttpSessionAttributeListener] | Interface for receiving notification events about https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] attribute changes.

| https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionBindingListener.html[HttpSessionBindingListener] | Interface for receiving notification events when an object is bound to or unbound from a https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession].


| https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionActivationListener.html[HttpSessionActivationListener] | Interface for receiving notification events when an https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] is being passivated and and activated.


| https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionIdListener.html[HttpSessionIdListener] | Interface for receiving notification events about https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] id changes.

|=== 

=== ServletContextListener

This interface is for receiving notification events about https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContext.html[ServletContext] lifecycle changes. Implementations of this interface are invoked at their https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContextListener.html#contextInitialized-javax.servlet.ServletContextEvent[contextInitialized] method in the order in which they have been declared, and at their https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContextListener.html#contextDestroyed-javax.servlet.ServletContextEvent-[contextDestroyed] method in reverse order.

.MyServletContextListener.java 
[#src-listing] 
[source,java]  
---- 
import javax.servlet.ServletContextEvent;
import javax.servlet.ServletContextListener;
import javax.servlet.annotation.WebListener;

@WebListener
public class MyServletContextListener implements ServletContextListener {
	
	public void contextInitialized(ServletContextEvent event) {
	   System.out.println("context started");	
	}
	
	public void contextDestroyed(ServletContextEvent event) {
	   System.out.println("context destroyed");
	}
	
}
----


.MyServletContextListener.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.ContextListener

@ContextListener
public class MyServletContextListener {
	
	void contextInitialized() {
	   println "context started"	
	}
	
	void contextDestroyed() {
	   println "context destroyed"
	}
	
}
----

The implicit variables made available to a https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContextListener.html[ServletContextListener] are as follows:

|===
|Variable |Description

|_logger_ | https://docs.oracle.com/javase/7/docs/api/java/util/logging/Logger.html[logger] object

|_context_ | https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContext.html[ServletContext] object

|_event_ | https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContextEvent.html[ServletContextEvent] object
|===

=== ServletContextAttributeListener

This interface is for receiving notification events about https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContext.html[ServletContext] attribute changes. The order in which implementations of this interface are invoked is unspecified.

.MyServletContextAttributeListener.java 
[#src-listing] 
[source,java]  
---- 
import javax.servlet.ServletContextAttributeEvent;
import javax.servlet.ServletContextAttributeListener;
import javax.servlet.annotation.WebListener;

@WebListener
public class MyServletContextAttributeListener implements ServletContextAttributeListener {

    public void attributeAdded(ServletContextAttributeEvent event) {
        System.out.println("attr " + event.getName() + " added with value " + event.getValue());
    }

    public void attributeRemoved(ServletContextAttributeEvent event) {
        System.out.println("attr " + event.getName() + " removed with value " + event.getValue());
    }

    public void attributeReplaced(ServletContextAttributeEvent event) {
        System.out.println("attr " + event.getName() + " replaced with value " + event.getValue());
    }
    
}
----

.MyServletContextAttributeListener.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.ContextAttributeListener

@ContextAttributeListener
public class MyServletContextAttributeListener {

    void attributeAdded() {
        println "attr $name added with value $value"
    }

    void attributeRemoved() {
        println "attr $name removed with value $value"
    }

    void attributeReplaced() {
        println "attr $name replaced with value $value"
    }
    
}
----

The implicit variables made available to a https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContextAttributeListener.html[ServletContextAttributeListener] are as follows:

|===
|Variable |Description

|_logger_ | https://docs.oracle.com/javase/7/docs/api/java/util/logging/Logger.html[logger] object

|_context_ | https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContext.html[ServletContext] object

|_event_ | https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContextAttributeEvent.html[ServletContextAttributeEvent] object

|_name_ | attribute name

|_value_ | attribute value
|===

=== ServletRequestListener

This interface is for receiving notification events about requests coming into and going out of scope of a web application. A request is defined as coming into scope of a web application when it is about to enter the first servlet or filter of the web application, and as going out of scope as it exits the last servlet or the first filter in the chain. Implementations of this interface are invoked at their https://docs.oracle.com/javaee/7/api/javax/servlet/ServletRequestListener.html#requestInitialized-javax.servlet.ServletRequestEvent[requestInitialized] method in the order in which they have been declared, and at their https://docs.oracle.com/javaee/7/api/javax/servlet/ServletRequestListener.html#requestDestroyed-javax.servlet.ServletRequestEvent-[requestDestroyed] method in reverse order.

.MyServletRequestListener.java 
[#src-listing] 
[source,java]  
---- 
import javax.servlet.ServletRequestEvent;
import javax.servlet.ServletRequestListener;
import javax.servlet.annotation.WebListener;

@WebListener
public class MyServletRequestListener implements ServletRequestListener {

    public void requestInitialized(ServletRequestEvent event) {
        System.out.println("request initialized");
    }

    public void requestDestroyed(ServletRequestEvent event) {
        System.out.println("request destroyed");
    }
    
}
----

.MyServletRequestListener.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.RequestListener

@RequestListener
public class MyServletRequestListener {

    void requestInitialized() {
        println "request initialized"
    }

    void requestDestroyed() {
        println "request destroyed"
    }
    
}
----

The implicit variables made available to a https://docs.oracle.com/javaee/7/api/javax/servlet/ServletRequestListener.html[ServletRequestListener] are as follows:

|===
|Variable |Description

|_logger_ | https://docs.oracle.com/javase/7/docs/api/java/util/logging/Logger.html[logger] object

|_request_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpServletRequest.html[HttpServletRequest] object

|_session_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] object

|_context_ | https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContext.html[ServletContext] object

|_event_ | https://docs.oracle.com/javaee/7/api/javax/servlet/ServletRequestEvent.html[ServletRequestEvent] object

|===

=== ServletRequestAttributeListener

This interface is for receiving notification events about https://docs.oracle.com/javaee/7/api/javax/servlet/ServletRequest.html[ServletRequest] attribute changes.
Notifications will be generated while the request is within the scope of the web application. A ServletRequest is defined as coming into scope of a web application when it is about to enter the first servlet or filter of the web application, and as going out of scope when it exits the last servlet or the first filter in the chain. The order in which implementations of this interface are invoked is unspecified.

.MyServletRequestAttributeListener.java 
[#src-listing] 
[source,java]  
---- 
import javax.servlet.ServletRequestAttributeEvent;
import javax.servlet.ServletRequestAttributeListener;
import javax.servlet.annotation.WebListener;

@WebListener
public class MyServletRequestAttributeListener implements ServletRequestAttributeListener {

    public void attributeAdded(ServletRequestAttributeEvent event) {
        System.out.println("attr " + event.getName() + " added with value " + event.getValue());
    }

    public void attributeRemoved(ServletRequestAttributeEvent event) {
        System.out.println("attr " + event.getName() + " removed with value " + event.getValue());
    }

    public void attributeReplaced(ServletRequestAttributeEvent event) {
        System.out.println("attr " + event.getName() + " replaced with value " + event.getValue());
    }
    
}
----

.MyServletRequestAttributeListener.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.RequestAttributeListener

@RequestAttributeListener
public class MyServletRequestAttributeListener {

    void attributeAdded() {
        println "attr $name added with value $value"
    }

    void attributeRemoved() {
        println "attr $name removed with value $value"
    }

    void attributeReplaced() {
        println "attr $name replaced with value $value"
    }
    
}
----

The implicit variables made available to a https://docs.oracle.com/javaee/7/api/javax/servlet/ServletRequestAttributeListener.html[ServletRequestAttributeListener] are as follows:

|===
|Variable |Description

|_logger_ | https://docs.oracle.com/javase/7/docs/api/java/util/logging/Logger.html[logger] object

|_request_ |  https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpServletRequest.html[HttpServletRequest] object

|_session_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] object

|_context_ | https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContext.html[ServletContext] object

|_event_ | https://docs.oracle.com/javaee/7/api/javax/servlet/ServletRequestAttributeEvent.html[ServletRequestAttributeEvent] object

|_name_ | attribute name

|_value_ | attribute value
|===

=== HttpSessionListener

This interface is for receiving notification events about https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] lifecycle changes. Implementations of this interface are invoked at their https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionListener.html#sessionCreated-javax.servlet.http.HttpSessionEvent[sessionCreated] method in the order in which they have been declared, and at their https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionListener.html#sessionDestroyed-javax.servlet.http.HttpSessionEvent[sessionDestroyed] method in reverse order.


.MyHttpSessionListener.java 
[#src-listing] 
[source,java]  
---- 
import javax.servlet.HttpSessionEvent;
import javax.servlet.HttpSessionListener;
import javax.servlet.annotation.WebListener;

@WebListener
public class MyHttpSessionListener implements HttpSessionListener {
	
	public void sessionCreated(HttpSessionEvent event) {
	  System.out.println("session created");	
	}
	
	public void sessionDestroyed(HttpSessionEvent event) {
	  System.out.println("session destroyed");
	}
	
}
----

.MyHttpSessionListener.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.SessionListener

@SessionListener
public class MyHttpSessionListener {
	
	void sessionCreated() {
	   println "session created"	
	}
	
	void sessionDestroyed() {
	   println "session destroyed"
	}
	
}
----

The implicit variables made available to a https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionListener.html[HttpSessionListener] are as follows:

|===
|Variable |Description

|_logger_ | https://docs.oracle.com/javase/7/docs/api/java/util/logging/Logger.html[logger] object

|_session_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] object

|_event_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionEvent.html[HttpSessionEvent] object

|===


=== HttpSessionAttributeListener

This interface is for receiving notification events about https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] attribute changes. The order in which implementations of this interface are invoked is unspecified.


.MyHttpSessionAttributeListener.java 
[#src-listing] 
[source,java]  
---- 
import javax.servlet.HttpSessionBindingEvent;
import javax.servlet.HttpSessionAttributeListener;
import javax.servlet.annotation.WebListener;

@WebListener
public class MyHttpSessionAttributeListener implements HttpSessionAttributeListener {

    public void attributeAdded(HttpSessionBindingEvent event) {
        System.out.println("attr " + event.getName() + " added with value " + event.getValue());
    }

    public void attributeRemoved(HttpSessionBindingEvent event) {
        System.out.println("attr " + event.getName() + " removed with value " + event.getValue());
    }

    public void attributeReplaced(HttpSessionBindingEvent event) {
        System.out.println("attr " + event.getName() + " replaced with value " + event.getValue());
    }
    
}
----


.MyHttpSessionAttributeListener.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.SessionAttributeListener

@SessionAttributeListener
public class MyHttpSessionAttributeListener {

    void attributeAdded() {
        println "attr $name added with value $value"
    }

    void attributeRemoved() {
        println "attr $name removed with value $value"
    }

    void attributeReplaced() {
        println "attr $name replaced with value $value"
    }
    
}
----

The implicit variables made available to a https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionAttributeListener.html[HttpSessionAttributeListener] are as follows:

|===
|Variable |Description

|_logger_ | https://docs.oracle.com/javase/7/docs/api/java/util/logging/Logger.html[logger] object

|_session_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] object

|_event_ |  https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionBindingEvent.html[HttpSessionBindingEvent] object

|_name_ | attribute name

|_value_ | attribute value
|===

=== HttpSessionBindingListener

This interface is for receiving notification events about when an object is bound to or unbound from a session. This may be as a result of a servlet programmer explicitly unbinding an attribute from a session, due to a session being invalidated, or due to a session timing out.

.MyHttpSessionBindingListener.java 
[#src-listing] 
[source,java]  
---- 
import javax.servlet.HttpSessionBindingEvent;
import javax.servlet.http.HttpSessionBindingListener;
import javax.servlet.annotation.WebListener;

@WebListener
public class MyHttpSessionBindingListener implements HttpSessionBindingListener {

    public void valueBound(HttpSessionBindingEvent event) {
        System.out.println("attr " + event.getName() + " bounded with value " + event.getValue());
    }

    public void valueUnbound(HttpSessionBindingEvent event) {
        System.out.println("attr " + event.getName() + " unbounded with value " + event.getValue());
    }
    
}
----

.MyHttpSessionBindingListener.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.SessionBindingListener

@SessionBindingListener
public class MyHttpSessionBindingListener {

    void valueBound() {
        println "attr $name bounded with value $value"
    }

    void valueUnbound() {
        println "attr $name unbounded with value $value"
    }
    
}
----

The implicit variables made available to a https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionBindingListener.html[HttpSessionBindingListener] are as follows:

|===
|Variable |Description

|_logger_ | https://docs.oracle.com/javase/7/docs/api/java/util/logging/Logger.html[logger] object

|_session_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] object

|_event_ |  https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionBindingEvent.html[HttpSessionBindingEvent] object

|_name_ | attribute name

|_value_ | attribute value
|===

=== HttpSessionActivationListener

Objects that are bound to a session may listen to container events notifying them that sessions will be passivated and activated. A container that migrates session between VMs or persists sessions is required to notify all attributes bound to sessions implementing this interface.

.MyHttpSessionActivationListener.java 
[#src-listing] 
[source,java]  
---- 
import javax.servlet.http.HttpSessionEvent;
import javax.servlet.http.HttpSessionActivationListener;
import javax.servlet.annotation.WebListener;

@WebListener
public class MyHttpSessionActivationListener implements HttpSessionActivationListener {

    public void sessionDidActivate(HttpSessionEvent event) {
        System.out.println("session activated");
    }

    public void sessionWillPassivate(HttpSessionEvent event) {
        System.out.println("session passivated");
    }
    
}
----

.MyHttpSessionActivationListener.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.SessionActivationListener

@SessionActivationListener
public class MyHttpSessionActivationListener {

    void sessionDidActivate() {
        println "session activated"
    }

    void sessionWillPassivate() {
        println "session passivated"
    }
    
}
----

The implicit variables made available to a https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionActivationListener.html[HttpSessionActivationListener] are as follows:

|===
|Variable |Description

|_logger_ | https://docs.oracle.com/javase/7/docs/api/java/util/logging/Logger.html[logger] object

|_session_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] object

|_event_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionEvent.html[HttpSessionEvent] object

|===

=== HttpSessionIdListener

This interface is for receiving notification events about HttpSession id changes. The order in which implementations of this interface are invoked is unspecified.

.MyHttpSessionIdListener.java 
[#src-listing] 
[source,java]  
---- 
import javax.servlet.http.HttpSessionEvent;
import javax.servlet.http.HttpSessionIdListener;
import javax.servlet.annotation.WebListener;

@WebListener
public class MyHttpSessionIdListener implements HttpSessionIdListener {

    public void sessionIdChanged(HttpSessionEvent event, String oldSessionId) {
        System.out.println("the session id was "+oldSessionId);
    }
    
}
----

.MyHttpSessionIdListener.groovy 
[#src-listing] 
[source,java]  
---- 
import org.servlet.annotation.SessionIdListener

@WebListener
public class MyHttpSessionIdListener {

    void sessionIdChanged() {
        println "the session id was $oldSessionId"
    }
    
}
----

The implicit variables made available to a https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionIdListener.html[HttpSessionIdListener] are as follows:

|===
|Variable |Description

|_logger_ | https://docs.oracle.com/javase/7/docs/api/java/util/logging/Logger.html[logger] object

|_session_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] object

|_event_ | https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSessionEvent.html[HttpSessionEvent] object

|_oldSessionId_ | old https://docs.oracle.com/javaee/7/api/javax/servlet/http/HttpSession.html[HttpSession] Id

|===

== Working With JSON

Groovy comes with integrated support for converting between Groovy objects and JSON. The classes dedicated to JSON serialisation and parsing are found in the *_groovy.json_* package. You can get an insight of how to use them in the Groovy https://groovy-lang.org/json.html[documentation]. In the GServlet API, we have simplified the process of parsing and producing JSON in your servlets and filters as below: 

.ProjectServlet.java 
[#src-listing] 
[source,java]  
----
import org.gservlet.annotation.Servlet

@Servlet("/projects")
class ProjectServlet {
   
   List projects = []

   void init() {
     projects << [id : 1, name : "Groovy", url : "https://groovy-lang.org"]
     projects << [id : 2, name : "Spring", url : "https://spring.io"]
     projects << [id : 3, name : "Maven",  url : "https://maven.apache.org"]
   }

   void get() {
     json(projects)
   }

   void post() {
     def project = request.body
     projects << project
     json(project)
   }
	
}
----

Whenever, the content type of the request is set to *_application/json_*, you can use its *_body_* property to get the payload as Groovy object. Your servlet or filter can use as well the built-in *_json()_* method to send JSON data as a response.

== Working With RDBMS

The *_groovy-sql_* module provides a higher-level abstraction over the current Javaâ€™s JDBC technology and it supports a wide variety of databases. To set up a database with the GServlet API is as simple as to create in the webapp directory, a file named *_gservlet.properties_*.

.gservlet.properties 
[#src-listing] 
[source,plain]  
---- 
db.driver : com.mysql.cj.jdbc.Driver
db.url : jdbc:mysql://localhost:3306/gservlet_examples
db.user : root
db.password : changeit
db.minPoolSize : 5
db.maxPoolSize : 10
----

For each request, an http://docs.groovy-lang.org/latest/html/api/groovy/sql/Sql.html[Sql] connection is automatically created from a data source and made available in your servlets and filters through the implicit _sql_ variable as below:

.ProjectServlet.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.Servlet

@Servlet("/projects")
class ProjectServlet {
	
   void post() {
     def params = [1, 'Groovy', 'https://groovy-lang.org']
     sql.execute 'insert into projects (id, name, url) values (?, ?, ?)', params   
   }
	
}
----

.ProjectFilter.groovy 
[#src-listing] 
[source,java]  
----  
import org.gservlet.annotation.Filter;
 
@Filter("/*")
class ProjectFilter {
 
   void filter() {
      sql.eachRow('select * from projects') { project ->
         println "${project.name.padRight(10)} ($project.url)"
      }
      next()
   }
     
}
----

After each request, the _close()_ method of the http://docs.groovy-lang.org/latest/html/api/groovy/sql/Sql.html[Sql] object is automatically invoked to bring it back to the connection pool. Below is a summary of the database configuration properties:

|===
|Name | Required | Description

| _db.driver_ | true | database driver class name

| _db.url_ | true | database url

| _db.user_ |  true | database user

| _db.password_ | true | database password
 
| _db.minPoolSize_ | false | database minimum pool size. The default value is 1

| _db.maxPoolSize_ | false | database maximum pool size. The default value is 3

|===

== Unit Testing

Unit testing servlets is difficult, therefore it is recommended to move the main business logic in the servlet into a separate class which has no dependencies on the Servlet API. The Groovy programming language comes with great support for writing tests. In addition to the language features and test integration with state-of-the-art testing libraries and frameworks, the Groovy ecosystem has born a rich set of testing libraries and frameworks. In its https://groovy-lang.org/testing.html[testing guide], you can take at a closer look at JUnit integration, Spock for specifications, and Geb for functional tests.

== Groovydocs Generation

https://github.com/apache/groovy/blob/master/subprojects/groovy-groovydoc/src/spec/doc/groovydoc.adoc[Groovydoc] was introduced in 2007 to provide for Groovy what https://docs.oracle.com/javase/8/docs/technotes/tools/windows/javadoc.html[Javadoc] provides for Java. It is used to generate the API documentation for the Groovy and Java classes that compose your project.

.ProjectServlet.groovy 
[#src-listing] 
[source,java]  
---- 
import org.gservlet.annotation.Servlet

/**
 * This servlet handles the management of projects
 * 
 * @author John Doe
 */
@Servlet(value="/projects")
class ProjectServlet {

	/**
	 * the projects list
	 */
	List projects = []

	/**
	 * the init method
	 */
	void init() {
		projects << [id : 1, name : "Groovy", url : "https://groovy-lang.org"]
		projects << [id : 2, name : "Spring", url : "https://spring.io"]
		projects << [id : 3, name : "Maven", url : "https://maven.apache.org"]
		projects << [id : 4, name : "JMeter", url : "https://jmeter.apache.org"]
	}
	
	/**
	 * handles the GET method
	 */
	void get() {
		json(projects)
	}

	/**
	 * handles the POST method
	 */
	void post() {
		def project = request.body
		projects << project
		json(project)
	}
	
}
----


=== Maven

https://github.com/groovy/GMavenPlus[GMavenPlus] is a rewrite of https://github.com/groovy/gmaven[GMaven], a plugin that allows you to integrate Groovy into your Maven projects. This is the basic configuration to generate Groovydocs with this plugin.

.pom.xml 
[#src-listing] 
[source,xml]  
---- 
<project>

	<dependencies>
		<dependency>
			<groupId>org.codehaus.groovy</groupId>
			<artifactId>groovy-all</artifactId>
			<version>3.0.6</version>
			<type>pom</type>
		</dependency>
	</dependencies>

	<build>
		<plugins>
			<plugin>
				<groupId>org.codehaus.gmavenplus</groupId>
				<artifactId>gmavenplus-plugin</artifactId>
				<version>1.11.0</version>
				<executions>
					<execution>
						<goals>
							<goal>groovydoc</goal>
						</goals>
					</execution>
				</executions>
				<configuration>
					<sources>
						<source>
							<directory>${project.basedir}/src/main/webapp/groovy</directory>
							<includes>
								<include>**/*.groovy</include>
							</includes>
						</source>
					</sources>
					<links>
					  <link>
					   <href>https://docs.oracle.com/javase/7/docs/api/</href>
					   <packages>java.,javax.,org.xml.</packages>
					  </link>
					  <link>
					   <href>http://docs.groovy-lang.org/latest/html/gapi/</href>
					   <packages>groovy.,org.codehaus.groovy.</packages>
					  </link>
					</links>
				</configuration>
			</plugin>
		</plugins>
	</build>

</project>
----

Run *_mvn gplus:groovydoc_* and your documentation will be generated in the *_target/gapidocs_* directory.

=== Gradle

With Gradle, You can use the https://github.com/apache/groovy/blob/master/subprojects/groovy-groovydoc/src/spec/doc/groovydoc.adoc[Groovydoc] tool to generate your project documentation, and the version that is used, is the one from the Groovy dependency defined in the build script.

.build.gradle 
[#src-listing] 
[source,java]  
---- 
repositories {
    mavenCentral()
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:3.0.6"
}

apply plugin: 'groovy'

sourceSets {
	main {
		groovy {
			srcDirs = ['src/main/webapp/groovy']
		}
	}
}

groovydoc {
  use = true
  groovyClasspath = configurations.compile 
  source = sourceSets.main.groovy
  link 'https://docs.oracle.com/javase/7/docs/api/', 'java.'
  link 'http://docs.groovy-lang.org/latest/html/gapi/', 'groovy.', 'org.codehaus.groovy.'
}
----

Run *_gradle groovydoc_* and your documentation will be generated in the *_build/docs/groovydoc_* directory.

== IDE Support

=== Eclipse

To augment the editor features such as content assist, the GServlet API ships with a DSL Descriptor (DSLD) to describe the editing semantics in a way that can be interpreted by the https://marketplace.eclipse.org/content/groovy-development-tools[Groovy Development Tools (GDT)] which provides Eclipse support for the Groovy programming language. 

.servlet.dsld 
[#src-listing] 
[source,java]  
----  
contribute(currentType(annos: annotatedBy(Servlet))) {
	property name : 'logger', type : java.util.Logger, provider : 'org.gservlet.AbstractServlet'
	property name : 'config', type : javax.servlet.ServletConfig, provider : 'org.gservlet.AbstractServlet'
	property name : 'request', type : javax.servlet.http.HttpServletRequest, provider : 'org.gservlet.AbstractServlet'
	property name : 'response', type : javax.servlet.http.HttpServletResponse, provider : 'org.gservlet.AbstractServlet'
	property name : 'session', type : javax.servlet.http.HttpSession, provider : 'org.gservlet.AbstractServlet'
	property name : 'context', type : javax.servlet.ServletContext, provider : 'org.gservlet.AbstractServlet'
	property name : 'sql', type : groovy.sql.Sql, provider : 'org.gservlet.AbstractServlet'
	property name : 'out', type : java.io.PrintWriter, provider : 'org.gservlet.AbstractServlet'
	property name : 'html', type : groovy.xml.MarkupBuilder, provider : 'org.gservlet.AbstractServlet'
}

contribute(currentType(annos: annotatedBy(Servlet))) {
	delegatesTo type : org.gservlet.AbstractServlet, except : [
		'service',
		'doGet',
		'doPost',
		'doHead',
		'doPut',
		'doTrace',
		'doOptions',
		'doDelete'
	]
}

contribute(currentType(annos: annotatedBy(Filter))) {
	property name : 'logger', type : java.util.Logger, provider : 'org.gservlet.AbstractFilter'
	property name : 'config', type : javax.servlet.FilterConfig, provider : 'org.gservlet.AbstractFilter'
	property name : 'request', type : javax.servlet.http.HttpServletRequest, provider : 'org.gservlet.AbstractFilter'
	property name : 'response', type : javax.servlet.http.HttpServletResponse, provider : 'org.gservlet.AbstractFilter'
	property name : 'chain', type : javax.servlet.FilterChain, provider : 'org.gservlet.AbstractFilter'
	property name : 'session', type : javax.servlet.http.HttpSession, provider : 'org.gservlet.AbstractFilter'
	property name : 'context', type : javax.servlet.ServletContext, provider : 'org.gservlet.AbstractFilter'
	property name : 'sql', type : groovy.sql.Sql, provider : 'org.gservlet.AbstractFilter'
	property name : 'out', type : java.io.PrintWriter, provider : 'org.gservlet.AbstractFilter'
	property name : 'html', type : groovy.xml.MarkupBuilder, provider : 'org.gservlet.AbstractFilter'
}

contribute(currentType(annos: annotatedBy(Filter))) {
	delegatesTo type : org.gservlet.AbstractFilter, except : ['init', 'doFilter']
}

----

Make sure that your project has the Groovy nature and the Groovy DSL support is activated. To leverage the full power of the https://marketplace.eclipse.org/content/groovy-development-tools[Groovy Development Tools (GDT)], it is highly recommended to add the _groovy_ folder as source folder, so you can use its wizards to create your artifacts. 

image::groovy-dsl-eclipse.png[Groovy DSL support]


NOTE: The use of the default package is discouraged and the example below illustrates a simple packaging practice to follow throughout your development for a cohesive structure of your project.

.ProjectDao.groovy 
[#src-listing] 
[source,java]  
---- 
package dao

class ProjectDao {
	
   List projects = []

   ProjectDao() {
     projects << [id : 1, name : "Groovy", url : "https://groovy-lang.org"]
     projects << [id : 2, name : "Spring", url : "https://spring.io"]
     projects << [id : 3, name : "Maven",  url : "https://maven.apache.org"]
   }
	
}
----


.ProjectServlet.java 
[#src-listing] 
[source,java]  
----
package servlets

import org.gservlet.annotation.Servlet
import dao.ProjectDao

@Servlet("/projects")
class ProjectServlet {
   
   ProjectDao projectDao

   void init() {
     projectDao = new ProjectDao()
   }

   void get() {
     json(projectDao.projects)
   }
	
}
----

=== IntelliJ IDEA

In the other hand, in IntelliJ IDEA, we can write GDSL Files to have code completion on the injected methods/properties and closures. https://confluence.jetbrains.com/display/GRVY/Scripting+IDE+for+DSL+awareness[GroovyDSL] is a scripting framework with a domain-specific language designed to define the behavior of end-user DSLs as script files which are executed by the IDE on the fly, bringing new reference resolution and code completion logic into the scope of a project.

.servlet.gdsl 
[#src-listing] 
[source,java]  
----  
def classContext = context(scope: classScope())

contributor(classContext) {
  if (hasAnnotation('org.gservlet.annotation.Servlet')) {
    property name: 'logger', type: 'java.util.logging.Logger'
    property name: 'config', type: 'javax.servlet.ServletConfig'
    property name: 'request', type: 'javax.servlet.http.HttpServletRequest'
    property name: 'response',type: 'javax.servlet.http.HttpServletResponse'
    property name: 'session', type: 'javax.servlet.http.HttpSession'
    property name: 'context', type: 'javax.servlet.ServletContext'
    property name: 'sql', type: 'groovy.sql.Sql'
    property name: 'out', type: 'java.io.PrintWriter'
    property name: 'html', type: 'groovy.xml.MarkupBuilder'
    delegatesTo(findClass('org.gservlet.AbstractServlet'))
  }
}

contributor(classContext) {
  if (hasAnnotation('org.gservlet.annotation.Filter')) {
    property name: 'logger', type: 'java.util.logging.Logger'
    property name: 'config', type: 'javax.servlet.FilterConfig'
    property name: 'request', type: 'javax.servlet.http.HttpServletRequest'
    property name: 'response', type: 'javax.servlet.http.HttpServletResponse'
    property name: 'chain', type: 'javax.servlet.FilterChain'
    property name: 'session', type: 'javax.servlet.http.HttpSession'
    property name: 'context', type: 'javax.servlet.ServletContext'
    property name: 'sql', type: 'groovy.sql.Sql'
    property name: 'out', type: 'java.io.PrintWriter'
    property name: 'html', type: 'groovy.xml.MarkupBuilder'
    delegatesTo(findClass('org.gservlet.AbstractFilter'))
  }
}
----

== Framework Support

=== Spring Boot

You just need this configuration below to leverage the GServlet API in your Spring Boot application. 

.SpringConfiguration.java 
[#src-listing] 
[source,java]  
----  
import javax.servlet.ServletContext;
import javax.servlet.ServletException;
import org.gservlet.GServletApplication;
import org.springframework.context.annotation.Configuration;
import org.springframework.context.annotation.Bean;

@Configuration
public class SpringConfiguration implements ServletContextInitializer {

	private GServletApplication application;
	
	@Override
	public void onStartup(ServletContext context) throws ServletException {
		application = new GServletApplication(context);
		application.startOnSpringBoot();
		
	}
	
	@Bean(destroyMethod = "stop")
	public GServletApplication servletApplication() {
		return application;
	}
	
}
----

If you want to reuse the data source configured by Spring, this is how you must proceed: 

.SpringConfiguration.java 
[#src-listing] 
[source,java]  
----  
import javax.servlet.ServletContext;
import javax.servlet.ServletException;
import javax.sql.DataSource;
import org.gservlet.GServletApplication;
import org.springframework.boot.web.servlet.ServletContextInitializer;
import org.springframework.context.annotation.Bean;
import org.springframework.context.annotation.Configuration;

@Configuration
public class SpringConfiguration implements ServletContextInitializer {

	private GServletApplication application;
	
	@Override
	public void onStartup(ServletContext context) throws ServletException {
		application = new GServletApplication(context);
		application.startOnSpringBoot();
		
	}
	
	@Bean(destroyMethod = "stop")
	public GServletApplication servletApplication(DataSource dataSource) {
		application.setDataSource(dataSource);
		return application;
	}
	
}
----

NOTE: Your Groovy scripts must be created in the *_src/main/resources_* folder if your application is packaged as a jar.

=== Other Java web frameworks 

If the target framework supports the _@WebListener_ annotation of the Servlet API, no configuration is required since we ship with a https://docs.oracle.com/javaee/7/api/javax/servlet/ServletContextListener.html[ServletContextListener] which starts and stops the application. This statement is valid for frameworks like JSF, Apache Struts.

.StartupListener.java 
[#src-listing] 
[source,java]  
----  
package org.gservlet;

import javax.servlet.ServletContextEvent;
import javax.servlet.ServletContextListener;
import javax.servlet.annotation.WebListener;

@WebListener
public class StartupListener implements ServletContextListener {

	private GServletApplication application; 

	@Override
	public void contextInitialized(ServletContextEvent event) {
		application = new GServletApplication(event.getServletContext());
		application.start();
	}
	
	@Override
	public void contextDestroyed(ServletContextEvent event) {
		application.stop();
	}
	
}
----

== Code Examples

We have created several code examples on https://github.com/GServlet/gservlet-examples[GitHub] to help beginners to learn and gain expertise at GServlet. Checkout the appropriate branch for the version that you are using.

== Copyright and License

Copyright @2020. Free use of this software is granted under the terms of the https://www.apache.org/licenses/LICENSE-2.0[Apache 2.0] License.

== Authors

GServlet was created by https://github.com/laminba2003[Mamadou Lamine Ba].

== Contributing

Contributions of any type or any scope, drive the project forward. There are lot of ways to contribute, not just code. We provide more information about how to get involved in our link:/contribute[contribute] page.
